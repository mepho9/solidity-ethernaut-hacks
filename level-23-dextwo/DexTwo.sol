// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "openzeppelin-contracts-08/token/ERC20/IERC20.sol";
import "openzeppelin-contracts-08/token/ERC20/ERC20.sol";
import "openzeppelin-contracts-08/access/Ownable.sol";

/*
    How the exploit works:

    DexTwo allows swapping tokens based on their relative balances, similar to Dex.
    But here, there is no restriction on which tokens can be swapped.

    The attack deploys two fake tokens (MyToken), controlled by the attacker.
    We mint a small amount of each and transfer one token to the Dex contract.
    Since DexTwo calculates swap amounts using the balances, our tokens manipulate the price.

    By swapping our fake tokens for the real tokens in the Dex,
    we drain both real tokens completely from the Dex contract.

    Steps:
    - Deploy two fake tokens and mint small amounts.
    - Transfer 1 token each to DexTwo.
    - Approve DexTwo to spend our fake tokens.
    - Swap our fake tokens for real tokens multiple times.
    - After swaps, DexTwoâ€™s balance of both real tokens is zero.

    To check success:
    token1.balanceOf(dex) == 0 && token2.balanceOf(dex) == 0
*/


contract DexTwo is Ownable {
    address public token1;
    address public token2;

    constructor() {}

    function setTokens(address _token1, address _token2) public onlyOwner {
        token1 = _token1;
        token2 = _token2;
    }

    function add_liquidity(address token_address, uint256 amount) public onlyOwner {
        IERC20(token_address).transferFrom(msg.sender, address(this), amount);
    }

    function swap(address from, address to, uint256 amount) public {
        require(IERC20(from).balanceOf(msg.sender) >= amount, "Not enough to swap");
        uint256 swapAmount = getSwapAmount(from, to, amount);
        IERC20(from).transferFrom(msg.sender, address(this), amount);
        IERC20(to).approve(address(this), swapAmount);
        IERC20(to).transferFrom(address(this), msg.sender, swapAmount);
    }

    function getSwapAmount(address from, address to, uint256 amount) public view returns (uint256) {
        return ((amount * IERC20(to).balanceOf(address(this))) / IERC20(from).balanceOf(address(this)));
    }

    function approve(address spender, uint256 amount) public {
        SwappableTokenTwo(token1).approve(msg.sender, spender, amount);
        SwappableTokenTwo(token2).approve(msg.sender, spender, amount);
    }

    function balanceOf(address token, address account) public view returns (uint256) {
        return IERC20(token).balanceOf(account);
    }
}

contract SwappableTokenTwo is ERC20 {
    address private _dex;

    constructor(address dexInstance, string memory name, string memory symbol, uint256 initialSupply)
        ERC20(name, symbol)
    {
        _mint(msg.sender, initialSupply);
        _dex = dexInstance;
    }

    function approve(address owner, address spender, uint256 amount) public {
        require(owner != _dex, "InvalidApprover");
        super._approve(owner, spender, amount);
    }
}